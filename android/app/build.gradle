plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    // Tracer plugin
    id "ru.ok.tracer"
}

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.climbingevents.app"
    compileSdk = 35
    ndkVersion = "25.1.8937393"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        applicationId = "com.climbingevents.app"
        minSdk = Math.max(flutter.minSdkVersion, 23)  // 23 required by flutter_rustore_push
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias = keystoreProperties["keyAlias"]
                keyPassword = keystoreProperties["keyPassword"]
                storeFile = rootProject.file(keystoreProperties["storeFile"])
                storePassword = keystoreProperties["storePassword"]
            }
        }
    }

    buildTypes {
        release {
            signingConfig = keystorePropertiesFile.exists() ? signingConfigs.release : signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        jniLibs.useLegacyPackaging = true
    }
}

flutter {
    source = "../.."
}

// Tracer configuration
tracer {
    create("defaultConfig") {
        // TODO: замените на реальные значения из Tracer
        pluginToken = "zOZzrZ7yzMwIXhHej94GOMxBBvOVDDLs2qHRBhQEgJj"
        appToken = "9F7gHqIsamI3XB8gU9znZLKUEpvqyrrfJMkwDH4jQN1"

        // Примеры настроек — подберите под свои нужды
        uploadMapping = true
        uploadNativeSymbols = true
        uploadRetryCount = 2
        // Если нужны нестандартные директории с либами:
        // additionalLibrariesPath = projectDir.toString() + "/aVeryNonstandardLibsDirectory"
    }
}

dependencies {
    // RuStore Push SDK
    implementation("ru.rustore.sdk:pushclient:6.10.0")
    // Платформа Tracer
    implementation platform("ru.ok.tracer:tracer-platform:1.1.7")

    // Сбор и анализ крэшей и ANR
    implementation "ru.ok.tracer:tracer-crash-report"
    // Сбор и анализ нативных крэшей
    implementation "ru.ok.tracer:tracer-crash-report-native"
    // Сбор и анализ хипдампов при OOM
    implementation "ru.ok.tracer:tracer-heap-dumps"
    // Анализ потребления дискового места
    implementation "ru.ok.tracer:tracer-disk-usage"
    // Семплирующий профайлер
    implementation "ru.ok.tracer:tracer-profiler-sampling"
    // Systrace-профайлер
    implementation "ru.ok.tracer:tracer-profiler-systrace"
}
